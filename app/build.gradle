apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'


def gitSha = 'git rev-parse --short HEAD'.execute([],project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([],project.rootDir).text.trim()
def gitCommitCount = 100 +
        Integer.parseInt('git rev-list --count HEAD'.execute([],project.rootDir).text.trim())

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "io.dawdleapp"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode gitCommitCount
        versionName gitTag
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "IMGUR_CLIENT_ID", "\"${imgur_client_id}\""
        buildConfigField "String", "IMGUR_CLIENT_SECRET", "\"${imgur_client_secret}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}



dependencies {

    final SUPPORT_LIBRARY_VERSION = "25.2.0"
    final OKHTTP_VERSION = "3.6.0"
    final DAGGER_VERSION = "2.8"
    final RETROFIT_VERSION = "2.2.0"
    final TIMBER_VERSION = "4.5.1"
    final BUTTERKNIFE_VERSION = "8.5.1"
    final RXBINDING_VERSION = "2.0.0"
    final GLIDE_VERSION = "3.7.0"


    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.0'
    compile "com.android.support:customtabs:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:palette-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"


    //Unified Logging - Timber (https://github.com/JakeWharton/timber)
    compile "com.jakewharton.timber:timber:${TIMBER_VERSION}"

    //View Injection - Butterknife (http://jakewharton.github.io/butterknife/)
    compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"

    //Dependency Injection - Dagger 2 (http://google.github.io/dagger/)
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"


    compile "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    //REST Client - Retrofit (http://square.github.io/retrofit/)
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"

    //HTTP/SPDY Client - OKHttp (http://square.github.io/okhttp/)
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile 'com.squareup.okio:okio:1.8.0'

    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile "com.jakewharton.rxbinding2:rxbinding:$RXBINDING_VERSION"
    compile "com.jakewharton.rxbinding2:rxbinding-design:$RXBINDING_VERSION"

    testCompile 'junit:junit:4.12'
}
